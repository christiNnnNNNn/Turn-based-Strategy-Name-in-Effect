import src/model
effect roll(maxExclusive: Int): Int
effect triangleBonus(attKind: WeaponKind, defKind: WeaponKind): (Int, Int)
effect modifyDamage(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon, dmg: Int): Int
effect modifyHit(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon, hit: Int): Int
effect modifyCrit(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon, crit: Int): Int


effect clamp0(n: Int): Int
extern def randInt(max: Int) at io: Int =
  jsNode "Math.floor(Math.random() * ${max})"


def manhattan(x1: Int, y1: Int, x2: Int, y2: Int): Int =
  abs(x1 - x2) + abs(y1 - y2)

record CombatPreview(
  dmg: Int,
  hit: Int,
  crit: Int
)

def preview(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon)
  : CombatPreview / { triangleBonus, clamp0, modifyDamage, modifyHit, modifyCrit } =
{
  val (triDmg, triHit) = do triangleBonus(wAtt.kind, wDef.kind)

  val atkStat =
    wAtt.damageType match {
      case Physical() => att.strength
      case Magic()    => att.magic
    }

  val defStat =
    wAtt.damageType match {
      case Physical() => deff.defense
      case Magic()    => deff.resistance
    }

  val baseDmg = wAtt.attack + atkStat - defStat + triDmg
  val dmg     = do clamp0(do modifyDamage(att, deff, wAtt, wDef, baseDmg))

  val baseHit = wAtt.hit + att.precision - deff.agility + triHit
  val hit     = do clamp0(do modifyHit(att, deff, wAtt, wDef, baseHit))

  val baseCrit = att.luck - deff.luck + wAtt.crit
  val crit     = do clamp0(do modifyCrit(att, deff, wAtt, wDef, baseCrit))

  CombatPreview(dmg, hit, crit)
}

record AttackResult(hit: Bool, crit: Bool, damage: Int)

def resolveAttack(att: gameUnbit, deff: gameUnbit, p: CombatPreview): AttackResult / { roll } =
  val h = do roll(100)
  if (h >= p.hit) AttackResult(false, false, 0)
  else {
    val c = do roll(100)
    val isCrit = c < p.crit
    val dmg = if (isCrit) p.dmg * 3 else p.dmg
    AttackResult(true, isCrit, dmg)
  }

def applyDamage(u: gameUnbit, dmg: Int): gameUnbit =
  val hp2 = max(0, u.hp - dmg)
  gameUnbit(
    u.side, u.x, u.y,
    hp2,
    u.strength, u.magic, u.defense, u.resistance,
    u.precision, u.agility, u.luck,
    u.movement,
    u.weapons,
    u.activeWeapon,
    u.moved,
    u.symbol
  )
def previewDefault(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon): CombatPreview / {} =
  try { preview(att, deff, wAtt, wDef) }
  with clamp0 { n => resume(if (n < 0) 0 else n) }
  with triangleBonus { (ak, dk) =>
    resume(
      (ak, dk) match {
        case (Sword(), Axe())   => (1, 15)
        case (Axe(), Lance())   => (1, 15)
        case (Lance(), Sword()) => (1, 15)
        case (Axe(), Sword())   => (-1, -15)
        case (Lance(), Axe())   => (-1, -15)
        case (Sword(), Lance()) => (-1, -15)
        case _ => (0, 0)
      }
    )
  }
  with modifyDamage { (a, d, wa, wd, dmg) => resume(dmg) }
  with modifyHit    { (a, d, wa, wd, hit) => resume(hit) }
  with modifyCrit   { (a, d, wa, wd, c)   => resume(c) }

def previewPure(att: gameUnbit, deff: gameUnbit, wAtt: Weapon, wDef: Weapon): CombatPreview / {} =
  try { preview(att, deff, wAtt, wDef) }
  with clamp0 { n => resume(if (n < 0) 0 else n) }
  with triangleBonus { (ak, dk) =>
    resume(
      (ak, dk) match {
        case (Sword(), Axe())   => (1, 15)
        case (Axe(), Lance())   => (1, 15)
        case (Lance(), Sword()) => (1, 15)
        case (Axe(), Sword())   => (-1, -15)
        case (Lance(), Axe())   => (-1, -15)
        case (Sword(), Lance()) => (-1, -15)
        case _                  => (0, 0)
      }
    )
  }
  with modifyDamage { (_, _, _, _, dmg) => resume(dmg) }
  with modifyHit    { (_, _, _, _, hit) => resume(hit) }
  with modifyCrit   { (_, _, _, _, c)   => resume(c) }


def alive(u: gameUnbit): Bool = u.hp > 0


